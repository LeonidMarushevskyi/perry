buildscript {
    repositories {
        jcenter()
        mavenCentral()
    }
    dependencies {
        classpath group: 'javax.interceptor', name: 'javax.interceptor-api', version: '1.2'
    }
}

plugins {
    id "java"
    id 'application'
    id "org.sonarqube" version "2.5"
    id 'org.springframework.boot' version '1.5.2.RELEASE'
}

apply plugin: 'java'
apply plugin: 'application'
apply plugin: 'maven-publish'
apply plugin: 'maven'
apply plugin: 'jacoco'

group = 'gov.ca.cwds.api'

description = """perry"""

sourceCompatibility = 1.8
targetCompatibility = 1.8

project.ext {
    configPath = "$rootProject.projectDir/config/"
    targetDockerHubOrganization = System.env.DOCKERHUB_ORG ?: 'cwds'
    projectMajorVersion = '3.6'
    mainclass = 'gov.ca.cwds.PerryApplication'
    buildEnv = System.env.BUILD_ENV ?: 'JENKINS'
    devDockerHost = System.env.DEV_DOCKER_HOST ?: '192.168.99.100'
    devDockerPort = 2376
    releaseDocker = System.getProperty("ReleaseDocker")
    buildNumber = System.getProperty("BuildNumber")
    projectVersion = projectMajorVersion + '-SNAPSHOT'
}

println "ReleaseDocker: " + releaseDocker
println "Build: " + buildNumber
println "Project Version: " + projectVersion
println "Project Major Version: " + projectMajorVersion

jacocoTestReport {
    reports {
        xml.enabled false
        csv.enabled false
        html.destination "${buildDir}/jacocoHtml"
    }
}

sonarqube {
    properties {
        property "sonar.jacoco.reportPath", "${buildDir}/jacoco/test.exec"
        property "sonar.jacoco.itReportPath", "${buildDir}/jacoco/integrationTest.exec"
    }
}

version = projectVersion
mainClassName = mainclass

bootRepackage {
    mainClass = 'gov.ca.cwds.PerryApplication'
}

repositories {
    mavenLocal()
    mavenCentral()
    maven {
        url "http://maven2.cwds.tabordasolutions.net"
    }
}

dependencies {
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-security', version:'1.5.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-web', version:'1.5.2.RELEASE'
    compile group: 'org.springframework.boot', name: 'spring-boot-starter-data-jpa', version: '1.5.3.RELEASE'

    compile group: 'org.springframework.security.oauth', name: 'spring-security-oauth2', version:'2.0.13.RELEASE'
    compile group: 'org.webjars', name: 'angularjs', version:'1.4.3'
    compile group: 'org.webjars', name: 'jquery', version:'2.1.1'
    compile group: 'org.webjars', name: 'bootstrap', version:'3.2.0'
    compile group: 'org.webjars', name: 'webjars-locator', version:'0.32'
    compile group: 'io.swagger', name: 'swagger-jersey2-jaxrs', version: '1.5.9'
    compile group: 'io.swagger', name: 'swagger-annotations', version: '1.5.9'
    compile group: 'org.codehaus.groovy', name: 'groovy-all', version: '2.4.11'
    //h2 dev mode
    compile group: 'com.h2database', name: 'h2', version: '1.4.196'

    compile fileTree(dir: 'lib', include: ['*.jar'])
    compile ('gov.ca.cwds.api:api-core:0.19.118') {
        exclude group: 'org.glassfish.jersey.core'
    }

    compile project(':jwt-security');

    compile group: 'javax.interceptor', name: 'javax.interceptor-api', version: '1.2'
    testCompile(group: 'org.springframework.boot', name: 'spring-boot-starter-test', version:'1.5.2.RELEASE') {
            exclude(module: 'commons-logging')
    }

    testCompile 'org.mockito:mockito-core:2.0.54-beta'
    testCompile group: 'org.hamcrest', name: 'hamcrest-junit', version: '2.0.0.0'
    testCompile('org.glassfish.jersey.test-framework.providers:jersey-test-framework-provider-grizzly2:2.23.2') {
        exclude group: 'javax.servlet', module: 'javax.servlet-api'
        exclude group: 'junit', module: 'junit'
    }

    // https://mvnrepository.com/artifact/nl.jqno.equalsverifier/equalsverifier
    testCompile group: 'nl.jqno.equalsverifier', name: 'equalsverifier', version: '2.1.6'
    testCompile 'org.ow2.asm:asm-all:5.1'
    testCompile group: 'io.dropwizard', name: 'dropwizard-testing', version: '1.0.5'
}

publish.dependsOn ':shiro-security:publish'

def isPortOpen(host, port) {
    try {
        Socket socket = new Socket(host, port)
        socket.close()
        return true
    } catch (IOException e) {
        return false
    }
}

// reused in sub-modules
task checkDockerService {
    enabled = project.buildEnv == 'WIN_DEV'
    doFirst {
        if (!isPortOpen(project.devDockerHost, project.devDockerPort)) {
            throw new GradleException('Docker service is not available. Windows users should start Docker Quickstart Terminal of Docker Toolbox')
        }
    }
    doLast {
        def dockerEnvVars = [:]
        dockerEnvVars['DOCKER_CERT_PATH'] = System.getProperty("user.home") + '/.docker/machine/machines/default'
        dockerEnvVars['DOCKER_HOST'] = "tcp://$project.devDockerHost:$project.devDockerPort"
        dockerEnvVars['DOCKER_MACHINE_NAME'] = 'default'
        dockerEnvVars['DOCKER_TLS_VERIFY'] = 1

        childProjects.each {
            k, childProject -> childProject.tasks.withType(Exec) { environment << dockerEnvVars }
        }
    }
}

task publishDocker(dependsOn: ':docker-perry:dockerPerryPublish') {
    doLast {
        println 'Published'
    }
}

javadoc {
    options.addStringOption("charset", "UTF-8")
}

task integrationTest(type: Test, dependsOn: testClasses) {
    include '**/*IT*'
    exclude '**/*Test*'
}
